apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayUser = resolveProperty( 'BINTRAY_USER', 'bintrayUser' )
    bintrayKey = resolveProperty( 'BINTRAY_KEY', 'bintrayKey' )
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
            artifact javadocJar
        }
    }
}

artifactoryPublish {
    onlyIf { isMaster }
    mustRunAfter 'build'
    publications 'mavenJava'
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'

    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayKey
            maven = true
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publish = true
    publications = ['mavenJava']

    pkg {
        name = "${project.name}"
        repo = 'maven'
        userOrg = 'srs'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/srs/gradle-node-plugin.git'
        websiteUrl = "https://github.com/srs/gradle-node-plugin"
        issueTrackerUrl = "https://github.com/srs/gradle-node-plugin/issues"
        githubRepo = "srs/gradle-node-plugin"
        labels = ['java', 'gradle', 'node', 'yarn', 'npm', 'grunt', 'gulp']
        desc = "Gradle plugin for executing node scripts."

        version {
            vcsTag = "v${project.version}"
            attributes = ['gradle-plugin': ["com.moowork.node:${project.group}:${project.name}",
                                            "com.moowork.grunt:${project.group}:${project.name}",
                                            "com.moowork.gulp:${project.group}:${project.name}"]]
        }
    }
}

String resolveProperty( String envVarKey, String projectPropKey )
{
    String propValue = System.getenv()[envVarKey]

    if ( propValue )
    {
        return propValue
    }

    hasProperty( projectPropKey ) ? getProperty( projectPropKey ) : null
}
