buildscript {
    apply from: "$rootDir/gradle/buildscript.gradle", to: buildscript
}

group = 'com.moowork.gradle'

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'
apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/coverage.gradle"
apply from: "$rootDir/gradle/travis-ci.gradle"
apply from: "$rootDir/gradle/publishing.gradle"

ext.compatibilityVersion = '1.8'
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

repositories {
    jcenter()
}

configurations {
    integTestCompile.extendsFrom testCompileClasspath
    integTestRuntime.extendsFrom testRuntimeClasspath
}

dependencies {
    implementation gradleApi()
    testImplementation 'cglib:cglib-nodep:3.2.4'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation( 'org.spockframework:spock-core:1.0-groovy-2.4' ) {
        exclude group: 'org.codehaus.groovy'
    }
}

tasks.withType( Test ) {
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }
}

sourceSets {
    integTest
}

task integTest( type: Test ) {
    shouldRunAfter test

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
}
